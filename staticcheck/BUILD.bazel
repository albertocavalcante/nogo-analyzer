load("@io_bazel_rules_go//go:def.bzl", "go_library", "go_test")
load("//staticcheck:def.bzl", "ANALYZERS", "staticcheck_analyzers")
load("//private:deps_test.bzl", "prune_ext_deps")
load("//private:gen_deps.bzl", "generate_deps")

# gazelle:lang test_filegroup

exports_files([
    "def.bzl",
    "deps.bzl",
])

[
    go_library(
        name = analyzer,
        srcs = ["analyzer.go"],
        importpath = "github.com/sluongng/nogo-analyzer/staticcheck/" + analyzer,
        visibility = ["//visibility:public"],
        x_defs = {"name": analyzer},
        deps = ["//staticcheck/util"],
    )
    for analyzer in ANALYZERS
]

[
    go_test(
        name = analyzer + "_test",
        size = "small",
        srcs = ["analyzer_test.go"],
        embed = [analyzer],
        # Only compatible with platforms which have sandbox execution support.
        # For more information see https://github.com/bazelbuild/rules_go/issues/3144
        target_compatible_with = select({
            "@platforms//os:osx": [],
            "@platforms//os:linux": [],
            "//conditions:default": ["@platforms//:incompatible"],
        }),
    )
    for analyzer in ANALYZERS
]

prune_ext_deps(
    name = "prune_deps",
    deps_file = "//staticcheck:deps.bzl",
    targets = staticcheck_analyzers(ANALYZERS, "//staticcheck"),
)

generate_deps(
    name = "staticcheck",
    mod_file = ":go.mod",
    sum_file = ":go.sum",
)

filegroup(
    name = "all_files",
    testonly = True,
    srcs = [
        "BUILD.bazel",
        "README.md",
        "analyzer.go",
        "analyzer_test.go",
        "def.bzl",
        "deps.bzl",
        "go.mod",
        "go.sum",
        "sample_config_json.txt",
        "//staticcheck/cmd:all_files",
        "//staticcheck/tests:all_files",
        "//staticcheck/util:all_files",
    ],
    visibility = ["//visibility:public"],
)
